1000 ! Electron moving along wire in competitive cellular automata
1010 ! Programming fundamentals were developed in research outlined in 
1020 ! Patrick Grim, Gary Mar, and Paul St. Denis, THE PHILOSOPHICAL 
1030 ! COMPUTER: EXPLORATORY ESSAYS IN PHILOSOPHICAL COMPUTER MODELING, 
1040 ! Bradford Books/MIT Press.  You are free to use and adapt the program
1050 ! for research purposes only, subject to the provision that this 
1060 ! message be retained in its entirety as part of your program.
1070 OPTION NOLET
1080 RANDOMIZE
1090 SET WINDOW 0,639,0,479
1100 dim score (4,4)
1110 bb = 2.412
1120 br = 2.534
1130 by = 2.472
1140 bp = 3.000
1150 rb = 2.4854
1160 rr = 2.412
1170 ry = 2.472
1180 rp = 2.542
1190 pb = 2.583
1200 pr = 2.567
1210 py = 2.472
1220 pp = 2.412
1230 yb = .868
1240 yr = .868
1250 yy = 2.667
1260 yp = .868
1270 score (1,1) = bb
1280 score (1,4) = by
1290 score (1,3) = bp
1300 score (1,2) = br
1310 score (2,1) = rb
1320 score (2,4) = ry
1330 score (2,3) = rp
1340 score (2,2) = rr
1350 score (3,1) = pb
1360 score (3,4) = py
1370 score (3,3) = pp
1380 score (3,2) = pr
1390 score (4,1) = yb
1400 score (4,4) = yy
1410 score (4,2) = yr
1420 score (4,3) = yp
1430 DIM HC(150)
1440 DIM SUM1SCORE(100)
1450 DIM SUM2SCORE(100)
1460 DIM RPICK(2,100)
1470 set color 15
1480 box area 0,640,300,480
1490 box area 300,640,0,480
1500 DIM NG(13,13)
1510 DIM OG(13,13)
1520 DIM LEFT(13)
1530 DIM RIGHT(13)
1540 DIM UPPER(13)
1550 DIM LOWER(13)
1560 DIM SC(100)
1570 DIM GRIDSCORE(13,13)
1580 DIM H(9)
1590 DIM S(9)
1600 rem set color 15
1610 rem set cursor 1,1
1620 rem print "TO EXIT PRESS SPACEBAR"
1630 FOR X = 1 TO 13
1640 FOR Y = 1 TO 13
1650 og(x,y) = 4
1660 next y
1670 next x
1680 for x = 1 to 13
1690 for y = 7 to 7
1700 og(x,y) = 1
1710 next y
1720 next x
1730 og(3,7) = 2
1740 og(2,7) = 3 
1750 MAT NG=OG
1760 FOR N = 1 TO 13
1770 LET LEFT(N)=N-1
1780 LET RIGHT(N)=N+1
1790 LET LOWER(N)=N-1
1800 LET UPPER(N)=N+1
1810 NEXT N
1820 LET LEFT(1)=13
1830 LET RIGHT(13)=1
1840 LET LOWER(1)=13
1850 LET UPPER(13)=1
1860 SET CURSOR 3, 42
1870 FOR X = 1 TO 13
1880 FOR Y = 1 TO 13
1890 LET SSCORE=SCORE(OG(X,Y),OG(X,LOWER(Y)))
1900 LET NSCORE=SCORE(OG(X,Y),OG(X,UPPER(Y)))
1910 LET WSCORE=SCORE(OG(X,Y),OG(LEFT(X),Y))
1920 LET ESCORE=SCORE(OG(X,Y),OG(RIGHT(X),Y))
1930 LET SWSCORE=SCORE(OG(X,Y),OG(LEFT(X),LOWER(Y)))
1940 LET NWSCORE=SCORE(OG(X,Y),OG(LEFT(X),UPPER(Y)))
1950 LET SESCORE=SCORE(OG(X,Y),OG(RIGHT(X),LOWER(Y)))
1960 LET NESCORE=SCORE(OG(X,Y),OG(RIGHT(X),UPPER(Y)))
1970 GRIDSCORE(X,Y)=NSCORE+SSCORE+ESCORE+WSCORE+NWSCORE+NESCORE+SWSCORE+SESCORE
1980 NEXT Y
1990 NEXT X
2000 FOR X = 1 TO 13
2010 FOR Y = 1 TO 13
2020 H(1)=GRIDSCORE(X,UPPER(Y))
2030 S(1)=OG(X,UPPER(Y))
2040 H(2)=GRIDSCORE(RIGHT(X),UPPER(Y))
2050 S(2)=OG(RIGHT(X),UPPER(Y))
2060 H(3)=GRIDSCORE(RIGHT(X),Y)
2070 S(3)=OG(RIGHT(X),Y)
2080 H(4)=GRIDSCORE(RIGHT(X),LOWER(Y))
2090 S(4)=OG(RIGHT(X),LOWER(Y))
2100 H(5)=GRIDSCORE((X),LOWER(Y))
2110 S(5)=OG(X,LOWER(Y))
2120 H(6)=GRIDSCORE(LEFT(X),LOWER(Y))
2130 S(6)=OG(LEFT(X),LOWER(Y))
2140 H(7)=GRIDSCORE(LEFT(X),Y)
2150 S(7)=OG(LEFT(X),Y)
2160 H(8)=GRIDSCORE(LEFT(X),UPPER(Y))
2170 S(8)=OG(LEFT(X),UPPER(Y))
2180 H(9)=GRIDSCORE(X,Y)
2190 S(9)=OG(X,Y)
2200 ST = INT(RND*8)+1
2210 FOR R= ST TO 8
2220 IF H(R)<HIGH THEN 2250
2230 HIGH=H(R)
2240 WINSTRAT=S(R)
2250 NEXT R
2260 FOR R = 1 TO ST-1
2270 IF H(R)<HIGH THEN 2300
2280 HIGH=H(R)
2290 WINSTRAT=S(R)
2300 NEXT R
2310 IF H(9)>= HIGH THEN WINSTRAT=S(9)
2320 LET OLDCOL=OG(X,Y)
2330 LET NEWCOL=WINSTRAT
2340 NG(X,Y)=WINSTRAT
2350 HIGH=0
2360 IF OG(X,Y) = 1 THEN SET COLOR 9
2370 IF OG(X,Y) = 4 THEN SET COLOR 14
2380 if og(x,y) = 3 then set color 13
2390 if og(x,y) = 2 then set color 12
2400 BOX AREA 4*X*5,4*X*5+15,4*Y*5,4*Y*5+15
2410 NEXT Y
2420 NEXT X
2430 MAT OG=NG
2440 goto 1860
2450 end
