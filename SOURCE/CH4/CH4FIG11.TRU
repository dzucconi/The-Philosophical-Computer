1000 REM Evolution with a variable number of random strategies, blocks
1010 REM of six dropped in each generation.  As in Chapter Four, Fig. 11
1020 REM Programming elements included here were developed in background 
1030 REM research for Patrick Grim, Gary Mar, and Paul St. Denis, THE 
1040 REM PHILOSOPHICAL COMPUTER: EXPLORATORY ESSAYS IN PHILOSOPHICAL 
1050 REM COMPUTER MODELING, Bradford Books/MIT press.  You are free to use 
1060 REM and adapt this program for research purposes only, subject to the 
1061 REM provision that this message be retained in its entirety as part of
1062 REM your program.
1180 RANDOMIZE
1190 SET WINDOW 0,639,0,479
1200 REM                        SETS ACTIVE WINDOW ON SCREEN
1210 REM    number of strategies (nos = 8)
1220 LET NOS = 8
1230 OPTION NOLET
1240 SET CURSOR 10,59
1250 PRINT "just a moment..."
1260 DIM ORDER(8)
1270 DIM PLAYERS (8,8)
1280 DIM STRATSCORE (8,8)
1290 DIM T2(8)
1300 DIM T3(8)
1310 DEF FNC(X,Y) = (T3(X) + (T2(X) - T3(X)) * T3(Y))/ (1-(T2(X)-T3(X))*(T2(Y)-T3(Y)))
1320 DEF FNZ(X,Y) = (T3(Y) + (T2(Y) - T3(Y)) * T3(X))/(1-(T2(X)-T3(X))*(T2(Y)-T3(Y)))
1330 DEF FNMATRIX(X,Y)= 1 + 4*FNZ(X,Y) - FNC(X,Y)- (FNC(X,Y)*FNZ(X,Y))
1340 DIM NG(64,64)
1350 DIM OG(64,64)
1360 DIM SCORE(8,8)
1370 DIM LEFT(64)
1380 DIM RIGHT(64)
1390 DIM UPPER(64)
1400 DIM LOWER(64)
1410 DIM SC(6000)
1420 DIM GRIDSCORE(64,64)
1430 DIM H(9)
1440 DIM S(9)
1450 FOR I = 1 TO NOS
1460  LET ORDER(I) = I
1470 NEXT I
1480 LET RAD = .01
1490 FOR X = 1 TO 64
1500  FOR Y = 1 TO 64
1510   ST=INT(RND*8)+1
1520   OG(X,Y)=ST
1530   SC(ST)=SC(ST)+1
1540  NEXT Y
1550 NEXT X
1560 MAT NG=OG
1570 FOR N = 1 TO 64
1580  LET LEFT(N)=N-1
1590  LET RIGHT(N)=N+1
1600  LET LOWER(N)=N-1
1610  LET UPPER(N)=N+1
1620 NEXT N
1630 LET LEFT(1)=64
1640 LET RIGHT(64)=1
1650 LET LOWER(1)=64
1660 LET UPPER(64)=1
1670 GOSUB 2620
1680  GOSUB 2360
1690  FOR X = 1 TO 64
1700   FOR Y = 1 TO 64
1710    LET SSCORE=SCORE(OG(X,Y),OG(X,LOWER(Y)))
1720    LET NSCORE=SCORE(OG(X,Y),OG(X,UPPER(Y)))
1730    LET WSCORE=SCORE(OG(X,Y),OG(LEFT(X),Y))
1740    LET ESCORE=SCORE(OG(X,Y),OG(RIGHT(X),Y))
1750    LET SWSCORE=SCORE(OG(X,Y),OG(LEFT(X),LOWER(Y)))
1760    LET NWSCORE=SCORE(OG(X,Y),OG(LEFT(X),UPPER(Y)))
1770    LET SESCORE=SCORE(OG(X,Y),OG(RIGHT(X),LOWER(Y)))
1780    LET NESCORE=SCORE(OG(X,Y),OG(RIGHT(X),UPPER(Y)))
1790    GRIDSCORE(X,Y)=NSCORE+SSCORE+ESCORE+WSCORE+NWSCORE+NESCORE+SWSCORE+SESCORE
1800   NEXT Y
1810  NEXT X
1820  FOR X = 1 TO 64
1830   FOR Y = 1 TO 64
1840    H(1)=GRIDSCORE(X,UPPER(Y))
1850    S(1)=OG(X,UPPER(Y))
1860    H(2)=GRIDSCORE(RIGHT(X),UPPER(Y))
1870    S(2)=OG(RIGHT(X),UPPER(Y))
1880    H(3)=GRIDSCORE(RIGHT(X),Y)
1890    S(3)=OG(RIGHT(X),Y)
1900    H(4)=GRIDSCORE(RIGHT(X),LOWER(Y))
1910    S(4)=OG(RIGHT(X),LOWER(Y))
1920    H(5)=GRIDSCORE((X),LOWER(Y))
1930    S(5)=OG(X,LOWER(Y))
1940    H(6)=GRIDSCORE(LEFT(X),LOWER(Y))
1950    S(6)=OG(LEFT(X),LOWER(Y))
1960    H(7)=GRIDSCORE(LEFT(X),Y)
1970    S(7)=OG(LEFT(X),Y)
1980    H(8)=GRIDSCORE(LEFT(X),UPPER(Y))
1990    S(8)=OG(LEFT(X),UPPER(Y))
2000    H(9)=GRIDSCORE(X,Y)
2010    S(9)=OG(X,Y)
2020    ST = INT(RND*8)+1
2030    FOR R= ST TO 8
2040     IF H(R)<HIGH THEN 2070
2050     HIGH=H(R)
2060     WINSTRAT=S(R)
2070    NEXT R
2080    FOR R = 1 TO ST-1
2090     IF H(R)<HIGH THEN 2120
2100     HIGH=H(R)
2110     WINSTRAT=S(R)
2120    NEXT R
2130    IF H(9)>= HIGH THEN WINSTRAT=S(9)
2140    LET OLDCOL=OG(X,Y)
2150    LET NEWCOL=WINSTRAT
2160    SC(OLDCOL)=SC(OLDCOL)-1
2170    NG(X,Y)=WINSTRAT
2180    SC(NEWCOL)=SC(NEWCOL)+1
2190    HIGH=0
2200    SET COLOR OG(X,Y)+1
2210    REM                 SETS CURRENT FOREGROUND COLOR
2220    BOX AREA X*7,X*7+5,Y*7,Y*7+5
2230    REM                 DRAWS RECTANGLE AND FILLS IT WITH FOREGROUND COLOR
2240    IF OG(X,Y)+1 < 16 THEN GOTO 2270
2250     SET COLOR 16
2260     BOX AREA X*7+2,X*7+3,Y*7+2,Y*7+3
2270   NEXT Y
2280  NEXT X
2290  MAT OG=NG
2300  REM       ASSIGNS THE WHOLE THE ARRAY NG TO THE ARRAY OG
2310  RZ = 1
2320  IF RZ = 1 THEN GOSUB 2760
2330  LET G = 0
2340 GOTO 1680
2350 REM --- print population and strategy ---
2360   GOSUB 3170
2370   SET COLOR 16
2380   BOX AREA 458,590,100,480 
2390   FOR L = 1 TO NOS
2400    IF SC(ORDER(L)) < 1 THEN GOTO 2500
2410    IF L > 25 THEN GOTO 2500
2420     SET COLOR ORDER(L)+1
2430     BOX AREA 458,463,480-(L*16+5),480-(L*16+10)
2440     IF ORDER(L)+1 < 16 THEN GOTO 2470
2450      SET COLOR 16
2460      BOX AREA 460,461,480-(L*16+7),480-(L*16+8)
2470     SET COLOR 3 
2480     SET CURSOR L+1,59
2490     PRINT USING " #.## #.## ####":T2(ORDER(L)),T3(ORDER(L)),SC(ORDER(L))
2500   NEXT L
2510   GEN = GEN+1
2520   SET CURSOR 26, 59
2530   PRINT GEN
2540 RETURN
2550   FOR X = 1 TO NOS
2560    FOR Y= 1 TO NOS
2570     LET SCORE(X,Y)=FNMATRIX(X,Y)
2580     LET SCORE(Y,X)=FNMATRIX(Y,X)
2590    NEXT Y
2600   NEXT X
2610 RETURN
2620   FOR P =1 TO NOS
2630    LET T2(P)=ROUND(RND,1)
2640    LET T3(P)=ROUND(RND,1)
2650    IF T2(P) = 0 THEN LET T2(P) = .01
2660    IF T2(P) = 1 THEN LET T2(P) = .99
2670    IF T3(P) = 0 THEN LET T3(P) = .01
2680    IF T3(P) = 1 THEN LET T3(P) = .99
2690   NEXT P
2700   FOR X= 1 TO NOS
2710    FOR Y= 1 TO NOS
2720     LET SCORE(X,Y) = FNMATRIX (X,Y)
2730    NEXT Y
2740   NEXT X
2750 RETURN
2760  REM  --- invasion of a cluster of 6 ---
2770  NOS = NOS + 1
2780  LET SC(NOS) = 0
2790  REM --- ds - dead strategy ---
2800  LET DS = 0
2810  DO
2820   DS = DS + 1
2830  LOOP WHILE SC(DS) >= 1
2840  LET X = INT(RND*64)+1
2850  LET Y = INT(RND*64)+1
2860  LET E = ROUND(RND,1)
2870  LET F = ROUND(RND,1)
2880  IF E = 0 THEN LET E = .01
2890  IF E = 1 THEN LET E = .99
2900  IF F = 0 THEN LET F = .01
2910  IF F = 1 THEN LET F = .99
2920  MAT REDIM T2(NOS)
2930  MAT REDIM T3(NOS)
2940  T2(DS) = E
2950  T3(DS) = F
2960  FOR I = 1 TO 3
2970   FOR J = 1 TO 2
2980    REM   --- xco - x-coordinate ---yco - y-coordinate ---
2990    LET XCO=X+I
3000    LET YCO=Y+J 
3010    IF XCO > 64 THEN LET XCO = XCO - 64
3020    IF YCO > 64 THEN LET YCO = YCO - 64
3030    LET HELP = OG(XCO,YCO) 
3040    LET SC(HELP) = SC(HELP) - 1
3050    LET OG(XCO,YCO) = DS
3060   NEXT J
3070  NEXT I
3080  IF NOS = DS THEN MAT REDIM ORDER(DS)
3090  IF DS = NOS THEN LET ORDER(DS) = DS
3100  IF DS < NOS THEN LET NOS = NOS - 1
3110  MAT REDIM SCORE(NOS,NOS)
3120  REM                   REDIMENSIONING OF THE ARRAY "SCORE"
3130  LET SC(DS) = 6
3140  GOSUB 2550
3150 RETURN
3160  REM --- ranking strategies ---
3170   FL = 0
3180   FOR I = 1 TO NOS-1
3190    IF SC(ORDER(I)) >= SC(ORDER(I+1)) THEN GOTO 3240
3200    RK = ORDER(I)
3210    ORDER(I) = ORDER(I+1)
3220    ORDER (I+1) = RK
3230    FL = 1
3240   NEXT I
3250  IF FL = 1 THEN 3170
3260  MAT REDIM ORDER(NOS)
3270  REM                   REDIMENSIONING OF THE ARRAY "ORDER"
3280  GOSUB 2550
3290 RETURN
3300 END
