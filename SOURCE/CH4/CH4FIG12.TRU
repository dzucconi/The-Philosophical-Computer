1000 REM Basic invasion program.  Allows experimentation with different 
1010 REM strategies invaded by single units of another strategy
1020 REM Programming elements included here were developed in background 
1030 REM research for Patrick Grim, Gary Mar, and Paul St. Denis, THE
1040 REM PHILOSOPHICAL COMPUTER: EXPLORATORY ESSAYS IN PHILOSOPHICAL
1050 REM COMPUTER MODELING, Bradford Books/MIT Press.  You are free to 
1060 REM use and adapt this program for research purposes only, subject to
1070 REM the provision that this message be retained in its entirety as 
1080 REM part of your program.
1081 SET WINDOW 0,639,0,479
1090 OPTION NOLET
1100 SET COLOR 3
1110 DIM PLAYERS (8,8)
1120 DIM STRATSCORE (8,8)
1130 DIM T2(8)
1140 DIM T3(8)
1150 DEF FNC(X,Y) = (T3(X) + (T2(X) - T3(X)) * T3(Y))/ (1-(T2(X)-T3(X))*(T2(Y)-T3(Y)))
1160 DEF FNZ(X,Y) = (T3(Y) + (T2(Y) - T3(Y)) * T3(X))/(1-(T2(X)-T3(X))*(T2(Y)-T3(Y)))
1170 DEF FNMATRIX(X,Y)= 1 + 4*FNZ(X,Y) - FNC(X,Y)- (FNC(X,Y)*FNZ(X,Y))
1180 REM           DEF - DEFINES FUNCTIONS
1190 DIM NG(26,26)
1200 DIM OG(26,26)
1210 DIM SCORE(8,8)
1220 DIM LEFT(26)
1230 DIM RIGHT(26)
1240 DIM UPPER(26)
1250 DIM LOWER(26)
1260 DIM SC(6000)
1270 DIM GRIDSCORE(26,26)
1280 DIM H(9)
1290 DIM S(9)
1300 LET RAD = .01
1310 FOR X = 1 TO 26
1320 FOR Y = 1 TO 26
1330 ST=INT(RND*8)+1
1340 OG(X,Y)=2
1350 SC(ST)=SC(ST)+1
1360 NEXT Y
1370 NEXT X
1380 OG(13,13) = 3
1390 MAT NG=OG
1400 FOR N = 1 TO 26
1410 LET LEFT(N)=N-1
1420 LET RIGHT(N)=N+1
1430 LET LOWER(N)=N-1
1440 LET UPPER(N)=N+1
1450 NEXT N
1460 LET LEFT(1)=26
1470 LET RIGHT(26)=1
1480 LET LOWER(1)=26
1490 LET UPPER(26)=1
1500 GOSUB 2370
1510 GEN = GEN+1
1520 SET CURSOR 12, 59
1530 REM              LOCATES CURSOR ON SCREEN
1540 PRINT GEN
1550 FOR X = 1 TO 26
1560 FOR Y = 1 TO 26
1570 LET SSCORE=SCORE(OG(X,Y),OG(X,LOWER(Y)))
1580 LET NSCORE=SCORE(OG(X,Y),OG(X,UPPER(Y)))
1590 LET WSCORE=SCORE(OG(X,Y),OG(LEFT(X),Y))
1600 LET ESCORE=SCORE(OG(X,Y),OG(RIGHT(X),Y))
1610 LET SWSCORE=SCORE(OG(X,Y),OG(LEFT(X),LOWER(Y)))
1620 LET NWSCORE=SCORE(OG(X,Y),OG(LEFT(X),UPPER(Y)))
1630 LET SESCORE=SCORE(OG(X,Y),OG(RIGHT(X),LOWER(Y)))
1640 LET NESCORE=SCORE(OG(X,Y),OG(RIGHT(X),UPPER(Y)))
1650 GRIDSCORE(X,Y)=NSCORE+SSCORE+ESCORE+WSCORE+NWSCORE+NESCORE+SWSCORE+SESCORE
1660 NEXT Y
1670 NEXT X
1680 FOR X = 1 TO 26
1690 FOR Y = 1 TO 26
1700 H(1)=GRIDSCORE(X,UPPER(Y))
1710 S(1)=OG(X,UPPER(Y))
1720 H(2)=GRIDSCORE(RIGHT(X),UPPER(Y))
1730 S(2)=OG(RIGHT(X),UPPER(Y))
1740 H(3)=GRIDSCORE(RIGHT(X),Y)
1750 S(3)=OG(RIGHT(X),Y)
1760 H(4)=GRIDSCORE(RIGHT(X),LOWER(Y))
1770 S(4)=OG(RIGHT(X),LOWER(Y))
1780 H(5)=GRIDSCORE((X),LOWER(Y))
1790 S(5)=OG(X,LOWER(Y))
1800 H(6)=GRIDSCORE(LEFT(X),LOWER(Y))
1810 S(6)=OG(LEFT(X),LOWER(Y))
1820 H(7)=GRIDSCORE(LEFT(X),Y)
1830 S(7)=OG(LEFT(X),Y)
1840 H(8)=GRIDSCORE(LEFT(X),UPPER(Y))
1850 S(8)=OG(LEFT(X),UPPER(Y))
1860 H(9)=GRIDSCORE(X,Y)
1870 S(9)=OG(X,Y)
1880 ST = INT(RND*8)+1
1890 FOR R= ST TO 8
1900 IF H(R)<HIGH THEN 1930
1910 HIGH=H(R)
1920 WINSTRAT=S(R)
1930 NEXT R
1940 FOR R = 1 TO ST-1
1950 IF H(R)<HIGH THEN 1980
1960 HIGH=H(R)
1970 WINSTRAT=S(R)
1980 NEXT R
1990 IF H(9)>= HIGH THEN WINSTRAT=S(9)
2000 LET OLDCOL=OG(X,Y)
2010 LET NEWCOL=WINSTRAT
2020 SC(OLDCOL)=SC(OLDCOL)-1
2030 NG(X,Y)=WINSTRAT
2040 SC(NEWCOL)=SC(NEWCOL)+1
2050 HIGH=0
2060 SET COLOR OG(X,Y)+1
2070 BOX AREA X*7,X*7+5,Y*7,Y*7+5
2080 REM                  DRAWS RECTANGLE AND FILLS IT WITH FOREGROUND COLOR
2090 NEXT Y
2100 NEXT X
2110 MAT OG=NG
2120 REM        ASSIGNS THE WHOLE ARRAY "NG" TO THE ARRAY "OG"
2130 GOTO 1510
2140 LET E =ROUND(RND,1)
2150 LET F =ROUND (RND,1)
2160 IF E = 0 THEN LET E = .01
2170 IF F = 0 THEN LET F = .01
2180 IF E = 1 THEN LET E = .99
2190 IF F = 1 THEN LET F = .99
2200 FOR X = 1 TO 64
2210 FOR Y = 1 TO 64
2220 SP = INT(RND*8)+1
2230 IF SP = 8 THEN GOSUB 2270 ELSE GOTO 2240
2240 NEXT Y
2250 NEXT X
2260 RETURN
2270 T2(L)=E
2280 T3(L)=F
2290 MUTSTRAT=OG(X,Y)
2300 SC(MUTSTRAT)=SC(MUTSTRAT)-1
2310 OG(X,Y)=L
2320 SC(OG(X,Y))=SC(OG(X,Y)) + 1
2330 RETURN
2340 X=L
2350 GOSUB 2510
2360 RETURN
2370 PRINT "First value of invaded strategy (between 0 and 1)";
2380 INPUT T2(2)
2390 PRINT "Second value of invaded strategy";
2400 INPUT T3(2)
2410 PRINT "First value of invader"; 
2420 INPUT T2(3)
2430 PRINT "Second value of invader";
2440 INPUT T3(3)
2450 FOR X= 1 TO 8
2460 FOR Y= 1 TO 8
2470 LET SCORE(X,Y) = FNMATRIX (X,Y)
2480 NEXT Y
2490 NEXT X
2500 RETURN
2510 FOR Y= 1 TO 8
2520 LET SCORE(X,Y)=FNMATRIX(X,Y)
2530 LET SCORE(Y,X)=FNMATRIX(Y,X)
2540 NEXT Y
2550 RETURN
2560 END
