1000 REM The Spatialized Stochastic Prisoner's Dilemma.  Eight initial 
1010 REM strategies with dead strategies replaced by randomly chosen 
1020 REM alternatives in 1/8 proportion
1030 REM Programming elements included here were developed in background 
1040 REM research for Patrick Grim, Gary Mar, and Paul St. Denis, THE 
1050 REM PHILOSOPHICAL COMPUTER: EXPLORATORY ESSAYS IN PHILOSOPHICAL 
1060 REM COMPUTER MODELING, Bradford Books/MIT press.  You are free to use 
1070 REM and adapt this program for research purposes only, subject to the 
1080 REM provision that this message be retained in its entirety as part of
1081 REM your program.
1090 RANDOMIZE
1100 SET WINDOW 0,639,0,479
1110 REM                     SETS CURRENT WINDOW
1120 OPTION NOLET
1130 GOSUB 2260
1140 DIM PLAYERS (8,8)
1150 DIM STRATSCORE (8,8)
1160 DIM T2(8)
1170 DIM T3(8)
1180 DEF FNC(X,Y) = (T3(X) + (T2(X) - T3(X)) * T3(Y))/ (1-(T2(X)-T3(X))*(T2(Y)-T3(Y)))
1190 DEF FNZ(X,Y) = (T3(Y) + (T2(Y) - T3(Y)) * T3(X))/(1-(T2(X)-T3(X))*(T2(Y)-T3(Y)))
1200 DEF FNMATRIX(X,Y)= 1 + 4*FNZ(X,Y) - FNC(X,Y)- (FNC(X,Y)*FNZ(X,Y))
1210 DIM NG(64,64)
1220 DIM OG(64,64)
1230 DIM SCORE(8,8)
1240 DIM LEFT(64)
1250 DIM RIGHT(64)
1260 DIM UPPER(64)
1270 DIM LOWER(64)
1280 DIM SC(6000)
1290 DIM GRIDSCORE(64,64)
1300 DIM H(9)
1310 DIM S(9)
1320 LET RAD = .01
1330 FOR X = 1 TO 64
1340 FOR Y = 1 TO 64
1350 ST=INT(RND*8)+1
1360 OG(X,Y)=ST
1370 SC(ST)=SC(ST)+1
1380 NEXT Y
1390 NEXT X
1400 MAT NG=OG
1410 FOR N = 1 TO 64
1420 LET LEFT(N)=N-1
1430 LET RIGHT(N)=N+1
1440 LET LOWER(N)=N-1
1450 LET UPPER(N)=N+1
1460 NEXT N
1470 LET LEFT(1)=64
1480 LET RIGHT(64)=1
1490 LET LOWER(1)=64
1500 LET UPPER(64)=1
1510 GOSUB 2540
1520 FOR L = 1 TO 8
1530 SET CURSOR L+1,59
1540 PRINT USING " #.## #.## ####":T2(L),T3(L),SC(L)
1550 NEXT L
1560 GEN = GEN+1
1570 SET CURSOR 12, 59
1580 PRINT GEN
1590 FOR X = 1 TO 64
1600 FOR Y = 1 TO 64
1610 LET SSCORE=SCORE(OG(X,Y),OG(X,LOWER(Y)))
1620 LET NSCORE=SCORE(OG(X,Y),OG(X,UPPER(Y)))
1630 LET WSCORE=SCORE(OG(X,Y),OG(LEFT(X),Y))
1640 LET ESCORE=SCORE(OG(X,Y),OG(RIGHT(X),Y))
1650 LET SWSCORE=SCORE(OG(X,Y),OG(LEFT(X),LOWER(Y)))
1660 LET NWSCORE=SCORE(OG(X,Y),OG(LEFT(X),UPPER(Y)))
1670 LET SESCORE=SCORE(OG(X,Y),OG(RIGHT(X),LOWER(Y)))
1680 LET NESCORE=SCORE(OG(X,Y),OG(RIGHT(X),UPPER(Y)))
1690 GRIDSCORE(X,Y)=NSCORE+SSCORE+ESCORE+WSCORE+NWSCORE+NESCORE+SWSCORE+SESCORE
1700 NEXT Y
1710 NEXT X
1720 FOR X = 1 TO 64
1730 FOR Y = 1 TO 64
1740 H(1)=GRIDSCORE(X,UPPER(Y))
1750 S(1)=OG(X,UPPER(Y))
1760 H(2)=GRIDSCORE(RIGHT(X),UPPER(Y))
1770 S(2)=OG(RIGHT(X),UPPER(Y))
1780 H(3)=GRIDSCORE(RIGHT(X),Y)
1790 S(3)=OG(RIGHT(X),Y)
1800 H(4)=GRIDSCORE(RIGHT(X),LOWER(Y))
1810 S(4)=OG(RIGHT(X),LOWER(Y))
1820 H(5)=GRIDSCORE((X),LOWER(Y))
1830 S(5)=OG(X,LOWER(Y))
1840 H(6)=GRIDSCORE(LEFT(X),LOWER(Y))
1850 S(6)=OG(LEFT(X),LOWER(Y))
1860 H(7)=GRIDSCORE(LEFT(X),Y)
1870 S(7)=OG(LEFT(X),Y)
1880 H(8)=GRIDSCORE(LEFT(X),UPPER(Y))
1890 S(8)=OG(LEFT(X),UPPER(Y))
1900 H(9)=GRIDSCORE(X,Y)
1910 S(9)=OG(X,Y)
1920 ST = INT(RND*8)+1
1930 FOR R= ST TO 8
1940 IF H(R)<HIGH THEN 1970
1950 HIGH=H(R)
1960 WINSTRAT=S(R)
1970 NEXT R
1980 FOR R = 1 TO ST-1
1990 IF H(R)<HIGH THEN 2020
2000 HIGH=H(R)
2010 WINSTRAT=S(R)
2020 NEXT R
2030 IF H(9)>= HIGH THEN WINSTRAT=S(9)
2040 LET OLDCOL=OG(X,Y)
2050 LET NEWCOL=WINSTRAT
2060 SC(OLDCOL)=SC(OLDCOL)-1
2070 NG(X,Y)=WINSTRAT
2080 SC(NEWCOL)=SC(NEWCOL)+1
2090 HIGH=0
2100 SET COLOR OG(X,Y)+1
2110 REM                    SETS FOREGROUND COLOR
2120 BOX AREA X*7,X*7+5,Y*7,Y*7+5
2130 REM                   DRAWS RECTANGLE & FILLS IT WITH FOREGROUND COLOR 
2140 NEXT Y
2150 NEXT X
2160 MAT OG=NG
2170 REM       ASSIGNS ALL OF THE ARRAY NG TO THE ARRAY OG
2180 FOR L = 1 TO 8
2190 IF SC(L) < 1 THEN LET G=1
2200 IF SC(L) < 1 THEN GOSUB 2310
2210 NEXT L
2220 FOR L = 1 TO 8
2230 IF G = 1 THEN GOSUB 2510
2240 NEXT L
2250 GOTO 1520
2260 FOR Y = 1 TO 8
2270 SET COLOR Y+1
2280 BOX AREA 458,463,480-(Y*16+5),480-(Y*16+10)
2290 NEXT Y
2300 RETURN
2310 LET E =ROUND(RND,1)
2320 LET F =ROUND (RND,1)
2330 IF E = 0 THEN LET E = .01
2340 IF F = 0 THEN LET F = .01
2350 IF E = 1 THEN LET E = .99
2360 IF F = 1 THEN LET F = .99
2370 FOR X = 1 TO 64
2380 FOR Y = 1 TO 64
2390 SP = INT(RND*8)+1
2400 IF SP = 8 THEN GOSUB 2440 ELSE GOTO 2410
2410 NEXT Y
2420 NEXT X
2430 RETURN
2440 T2(L)=E
2450 T3(L)=F
2460 MUTSTRAT=OG(X,Y)
2470 SC(MUTSTRAT)=SC(MUTSTRAT)-1
2480 OG(X,Y)=L
2490 SC(OG(X,Y))=SC(OG(X,Y)) + 1
2500 RETURN
2510 X=L
2520 GOSUB 2680
2530 RETURN
2540 FOR P =1 TO 8
2550 LET T2(P)=ROUND(RND,1)
2560 LET T3(P)=ROUND(RND,1)
2570 IF T2(P) = 0 THEN LET T2(P) = .01
2580 IF T2(P) = 1 THEN LET T2(P) = .99
2590 IF T3(P) = 0 THEN LET T3(P) = .01
2600 IF T3(P) = 1 THEN LET T3(P) = .99
2610 NEXT P
2620 FOR X= 1 TO 8
2630 FOR Y= 1 TO 8
2640 LET SCORE(X,Y) = FNMATRIX (X,Y)
2650 NEXT Y
2660 NEXT X
2670 RETURN
2680 FOR Y= 1 TO 8
2690 LET SCORE(X,Y)=FNMATRIX(X,Y)
2700 LET SCORE(Y,X)=FNMATRIX(Y,X)
2710 NEXT Y
2720 RETURN
2730 END
