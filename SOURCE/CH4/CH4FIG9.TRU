1000 REM Invasion of <0.9999999, 0.3333333> by a 16-block unit of 
1010 REM <0.9999999,0.9999999>
1020 REM Programming elements included here were developed in background 
1030 REM research for Patrick Grim, Gary Mar, and Paul St. Denis, THE
1040 REM PHILOSOPHICAL COMPUTER: EXPLORATORY ESSAYS IN PHILOSOPHICAL
1050 REM COMPUTER MODELING, Bradford Books/MIT Press.  You are free to 
1060 REM use and adapt this program for research purposes only, subject to
1070 REM the provision that this message be retained in its entirety as 
1080 REM part of your program.
1081 REM Glitch Note: This program may have memory problems running on newer
1082 REM versions of Trubasic
1090 rem for t = 1 to 16400
1100 rem let r = rnd
1110 rem next t
1120 SET WINDOW 0,639,0,479
1130 OPTION NOLET
1140 DIM SCORE(121,121)
1150 open #1:name "nowscor2",create old,org record,recsize 10
1160 mat read #1:score
1170 dim t2(121)
1180 dim t3(121)
1190 for x = 0 to 1 step .1
1200 for y = 0 to 1 step .1
1210 p=p+1
1220 t2(p)=x
1230 t3(p)=y
1240 next y
1250 next x
1260 for t = 1 to 121
1270 if t2(t) = 0 then let t2(t) = .01
1280 if t2(t) > .99 then let t2(t) = .99
1290 if t3(t) = 0 then let t3(t) = .01
1300 if t3(t) > .99 then let t3(t) = .99
1310 next t
1320 dim pop(121)
1330 DIM LASTPICK(2)
1340 DIM PICK(2)
1350 DIM NG(100,100)
1360 DIM OG(100,100)
1370 DIM LEFT(100)
1380 DIM RIGHT(100)
1390 DIM UPPER(100)
1400 DIM LOWER(100)
1410 dim order(121)
1420 for i = 1 to 121
1430 order(i)=i
1440 next i
1450 DIM GRIDSCORE(100,100)
1460 DIM H(9)
1470 DIM S(9)
1480 dim rn(1000)
1490 for x = 1 to 100
1500 for y = 1 to 100
1510 rp=int(rnd*121)+1
1520 og(x,y)=rp
1530 pop(rp)=pop(rp)+1
1540 NEXT y
1550 NEXT x
1560 GOSUB 2950
1570 MAT NG=OG
1580 FOR N = 1 TO 100
1590 LET LEFT(N)=N-1
1600 LET RIGHT(N)=N+1
1610 LET lower(N)=N-1
1620 LET upper(N)=N+1
1630 NEXT N
1640 LET LEFT(1)=100
1650 LET RIGHT(100)=1
1660 LET lower(1)=100
1670 LET upper(100)=1
1680 set color white
1690 for i = 1 to 28
1700 set cursor i+1,58
1710 print using "#.## #.## ####":t2(order(i)),t3(order(i)),pop(order(i))
1720 next i
1730 FOR X = 1 TO 100
1740 FOR Y = 1 TO 100
1750 LET SSCORE=SCORE(OG(X,Y),OG(X,lower(Y)))
1760 LET NSCORE=SCORE(OG(X,Y),OG(X,upper(Y)))
1770 LET WSCORE=SCORE(OG(X,Y),OG(LEFT(X),Y))
1780 LET ESCORE=SCORE(OG(X,Y),OG(RIGHT(X),Y))
1790 LET SWSCORE=SCORE(OG(X,Y),OG(LEFT(X),lower(Y)))
1800 LET NWSCORE=SCORE(OG(X,Y),OG(left(X),UPPER(Y)))
1810 LET SESCORE=SCORE(OG(X,Y),OG(right(X),LOWER(Y)))
1820 LET NESCORE=SCORE(OG(X,Y),OG(RIGHT(X),upper(Y)))
1830 GRIDSCORE(X,Y)=NSCORE+SSCORE+ESCORE+WSCORE+NWSCORE+NESCORE+SWSCORE+SESCORE
1840 NEXT Y
1850 NEXT X
1860 FOR X = 1 TO 100
1870 FOR Y = 1 TO 100
1880 H(1)=GRIDSCORE(X,UPPER(Y))
1890 S(1)=OG(X,UPPER(Y))
1900 H(2)=GRIDSCORE(RIGHT(X),UPPER(Y))
1910 S(2)=OG(RIGHT(X),UPPER(Y))
1920 H(3)=GRIDSCORE(right(X),Y)
1930 S(3)=OG(right(X),Y)
1940 H(4)=GRIDSCORE(RIGHT(X),LOWER(Y))
1950 S(4)=OG(RIGHT(X),LOWER(Y))
1960 H(5)=GRIDSCORE(x,lower(y))
1970 S(5)=OG(x,lower(y))
1980 H(6)=GRIDSCORE(left(X),lower(Y))
1990 S(6)=OG(left(x),lower(Y))
2000 H(7)=GRIDSCORE(left(X),Y)
2010 S(7)=OG(left(X),Y)
2020 H(8)=GRIDSCORE(left(X),upper(Y))
2030 S(8)=og(left(X),upper(Y))
2040 H(9)=GRIDSCORE(X,Y)
2050 S(9)=OG(X,Y)
2060 ST = INT(RND*8)+1
2070 FOR R= st to 8
2080 IF H(R)<HIGH THEN 2110
2090 HIGH=H(R)
2100 WINSTRAT=S(R)
2110 NEXT R
2120 FOR R = 1 to st-1
2130 IF H(R)<HIGH THEN 2160
2140 HIGH=H(R)
2150 WINSTRAT=S(R)
2160 NEXT R
2170 IF H(9)>= HIGH THEN winstrat=s(9)
2180 let oldcol=og(x,y)
2190 let newcol=winstrat
2200 pop(oldcol)=pop(oldcol)-1
2210 NG(X,Y)=WINSTRAT
2220 pop(newcol)=pop(newcol)+1
2230 HIGH=0
2240 LET C = OG(X,Y)+1
2250 LET A = X*4
2260 LET B = Y*4+50
2270 IF C < 16 THEN GOSUB 2380
2280 IF C>15 AND C<32 THEN GOSUB 2410
2290 IF C>31 AND C<48 THEN GOSUB 2460
2300 IF C>47 AND C<64 THEN GOSUB 2510
2310 IF C>63 AND C<80 THEN GOSUB 2560
2320 IF C>79 AND C<96 THEN GOSUB 2610
2330 IF C>95 AND C<112 THEN GOSUB 2660
2340 IF C>111 THEN GOSUB 2710
2350 NEXT Y
2360 NEXT X
2370 GOTO 2760
2380 SET COLOR C
2390 BOX AREA A,A+3,B,B+3
2400 RETURN
2410 SET COLOR C
2420 BOX AREA A,A+3,B,B+3
2430 IF C = 25 THEN SET COLOR 0 ELSE SET COLOR 9
2440 plot a+2,B+2
2450 RETURN
2460 SET COLOR C
2470 BOX AREA A,A+3,B,B+3
2480 IF C = 37 THEN SET COLOR 0 ELSE SET COLOR 5
2490 plot a+2,B+2
2500 RETURN
2510 SET COLOR C
2520 BOX AREA A,A+3,B,B+3
2530 IF C = 50 THEN SET COLOR 0 ELSE SET COLOR 2
2540 plot a+2,B+2
2550 RETURN
2560 SET COLOR C
2570 BOX AREA A,A+3,B,B+3
2580 IF C = 78 THEN SET COLOR 0 ELSE SET COLOR 14
2590 plot a+2,B+2
2600 RETURN
2610 SET COLOR C
2620 BOX AREA A,A+3,B,B+3
2630 IF C = 83 THEN SET COLOR 0 ELSE SET COLOR 3
2640 plot a+2,B+2
2650 RETURN
2660 SET COLOR C
2670 BOX AREA A,A+3,B,B+3
2680 IF C = 108 THEN SET COLOR 0 ELSE SET COLOR 12
2690 plot a+2,B+2
2700 RETURN
2710 SET COLOR C
2720 BOX AREA A,A+3,B,B+3
2730 SET COLOR 15
2740 plot  a+2,B+2
2750 RETURN
2760 MAT OG=NG
2770 GOSUB 2950
2780 FOR Y = 1 TO 25
2790 LET C3 = ORDER(Y)+1
2800 IF C3 < 16 THEN LET C2 = C3
2810 IF C3>15 AND C3<32 THEN LET C2 = 9
2820 IF C3>31 AND C3<48 THEN LET C2 = 5
2830 IF C3>47 AND C3<64 THEN LET C2 = 2
2840 IF C3>63 AND C3<80 THEN LET C2 = 14
2850 IF C3>79 AND C3<96 THEN LET C2 = 3
2860 IF C3>95 AND C3<112 THEN LET C2 = 12
2870 IF C3>111 THEN LET C2 = 15
2880 IF C3=25 OR C3=37 OR C3=50 OR C3=78 OR C3=83 OR C3=108 THEN LET C2=0
2890 SET COLOR C3
2900 BOX AREA 438,443,480-(Y*16+5),480-(Y*16+10)
2910 SET COLOR C2
2920 BOX AREA 440,441,480-(Y*16+7),480-(Y*16+9)
2930 NEXT Y
2940 goto 1690
2950 fl=0
2960 for i = 1 to 120 
2970 if pop(order(i))=>pop(order(i+1)) then 3020
2980 rk= order(i)
2990 order(i)= order(i+1)
3000 order(i+1)=rk
3010 fl=1
3020 next i
3030 if fl=1 then 2950
3040 RETURN
3050 END
